* File      : /home/pss060/sls/flechsig/phase/utils/show-mirror.kumac
* Date      : <30 Mar 01 16:26:01 flechsig> 
* Time-stamp: <02 Apr 01 10:52:53 flechsig> 
* Author    : Flechsig Uwe OVGA/203a 4535, flechsig@psi.ch
*********************************************************************
* display the surface by reading a PHASE coefficient file
* if there is no input we apply default mirror dimensions
* units: mm
* the histogram 1111 is generated and can be shown in different ways
*********************************************************************
macro show-mirror fn=! wmin=! wmax=! lmin=! lmax=!

on error goto exit
if [fn]  = '!' goto exit

*set defaults
if [wmin]= '!' then
   wmin=-100
endif
if [wmax]= '!' then
   wmax= 100
endif
if [lmin]= '!' then
   lmin=-10
endif
if [lmax]= '!' then
   lmax= 10
endif

ve/del *                          | delete all vectors
ve/re i,j,aa [fn] ! ! -/#/        | read file skip comments
ve/cr a(6,6) R

k=$sigma(nco(aa))                 | fill matrix
while [k] > 0 do
  i=i([k])+1
  j=j([k])+1
  ve/input a([i],[j]) aa([k])
  k= [k]-1
endwhile

application comis quit
	function su(x,y)
	vector a
	
	sum=0
	do i=1,6
	  do j=1,6
	    sum=sum+a(i,j)* x**(i-1)*y**(j-1)
	  enddo
        enddo	
	su=sum
	return
	end

quit

fun2 1111 su 100 [wmin] [wmax] 100 [lmin] [lmax] ' '     | create histogram

titel= file: [fn]
title [titel]
surf 1111 30 30 3                            | plot histogram
atitle 'w (mm)' 'l (mm)' 'u (mm)'

exitm                                        | normal exit

exit:
mess ' '
mess '***  EXPECTED ARGUMENTS: filename [wnin] [wmax] [lmin] [lmax] ***'
mess ' '